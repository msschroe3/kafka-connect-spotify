buildscript {
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:$shadow_version"
    }
}

plugins {
    id 'idea'
    id 'com.palantir.git-version' version '0.5.2'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

// configuration for all projects
allprojects {
    group = 'com.mschroeder.kafka.connect.spotify'
    version = versionDetails().lastTag.replaceAll(/^v/,'')

    repositories {
        jcenter()
    }

    apply plugin: 'kotlin'
    compileKotlin {
        kotlinOptions {
            jvmTarget = '1.8'
            javaParameters = true
        }
    }
    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    }
}

// shadow plugin: http://imperceptiblethoughts.com/shadow
shadowJar {
    dependencies {
        // exclude dependencies provided in the kafka connect classpath
        exclude dependency("org.apache.kafka:connect-api:$kafka_version")
        exclude dependency("org.apache.kafka:kafka-clients:$kafka_version")
        exclude dependency('net.jpountz.lz4:.*:.*')
        exclude dependency('org.xerial.snappy:.*:.*')
        exclude dependency('org.slf4j:.*:.*')
    }
}

// pull in sub-project dependencies
dependencies {
    compile project(':source')
    // compile project(':sink')
}
